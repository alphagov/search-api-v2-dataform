config {
    type: "operations",
    schema: "search_api",
    name: "view-item-intraday"
}

js {
    const getGa4TableDate = () => {
        const date = new Date();
        const year = date.getFullYear();
        const month = String(date.getMonth() + 1).padStart(2, '0');
        const day = String(date.getDate()).padStart(2, '0');
        return `${year}${month}${day}`;
    };
}

MERGE INTO
  `${dataform.projectConfig.vars.project_id}.analytics_events_vertex.view-item-intraday-event` T
USING
  (
  SELECT
    TIMESTAMP_TRUNC(TIMESTAMP_MICROS(ga.event_timestamp),DAY) AS _PARTITIONTIME,
    'view-item' AS eventType,
    ga.user_pseudo_id AS userPseudoId,
    FORMAT_TIMESTAMP("%FT%TZ",TIMESTAMP_MICROS(ga.event_timestamp)) AS eventTime,
    (CASE
        WHEN params.value.string_value IS NOT NULL THEN [STRUCT(params.value.string_value AS id, CAST(NULL AS string) AS name)]
    END
      ) AS documents
  FROM
    `ga4-analytics-352613.analytics_330577055.events_intraday_${getGa4TableDate()}` ga,
    UNNEST(event_params) AS params
  WHERE
    ga.event_name='page_view'
    AND params.key='content_id') S
ON
  T._PARTITIONTIME = S._PARTITIONTIME
  AND T.eventType = S.eventType
  AND T.userPseudoId = S.userPseudoId
  AND T.eventTime = S.eventTime
  AND TO_JSON_STRING(T.documents) = TO_JSON_STRING(S.documents)
  -- and T.documents.id = S.documents.id and T.documents.name = S.documents.name
  WHEN NOT MATCHED
  THEN
INSERT
  (_PARTITIONTIME,
    eventType,
    userPseudoId,
    eventTime,
    documents)
VALUES
  (_PARTITIONTIME, eventType, userPseudoId, eventTime, documents)

